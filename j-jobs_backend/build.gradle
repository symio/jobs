plugins {
	id 'java'
        id 'war'
	id 'org.springframework.boot' version '3.5.5'
	id 'io.spring.dependency-management' version '1.1.7'
}

group = 'org.loamok'
version = '0.0.11-SNAPSHOT'
description = 'Api « Jobs » java backend'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(21)
	}
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

// Configuration de la stratégie de résolution pour toutes les configurations
configurations.all {
        resolutionStrategy {
                // Seulement en développement
                if (project.hasProperty('dev') || System.getenv('ENV') == 'dev') {
                        cacheChangingModulesFor 0, 'seconds'
                        cacheDynamicVersionsFor 0, 'seconds'
                } else {
                        // En production : cache de 24h pour les SNAPSHOT
                        cacheChangingModulesFor 24, 'hours'
                }
        }
}

repositories {
        mavenLocal()
	mavenCentral()
        maven {
            name = "GitHubPackages-Loamok"
            url = uri("https://maven.pkg.github.com/symio/loamok-java-libs") 
            credentials {
                username = project.findProperty("gpr.user") ?: System.getenv("GITHUB_USERNAME")
                password = project.findProperty("gpr.key") ?: System.getenv("GITHUB_TOKEN")
            }
        }
}

dependencies {
        // Loamok
        implementation 'org.loamok.libs:o2springsecurity:1.0.7'
	// Lombok
        compileOnly("org.projectlombok:lombok:1.18.38")
        annotationProcessor("org.projectlombok:lombok:1.18.38")
        testCompileOnly("org.projectlombok:lombok:1.18.38")
	testAnnotationProcessor("org.projectlombok:lombok:1.18.38")
	// starters nécessaires au projet
	implementation 'org.springframework.boot:spring-boot-starter-data-rest'
	implementation 'org.springframework.boot:spring-boot-starter-security'
	implementation 'org.springframework.boot:spring-boot-starter-validation'
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
        implementation 'com.fasterxml.jackson.datatype:jackson-datatype-hibernate6'
	// Bases de données
	runtimeOnly 'com.h2database:h2'
	runtimeOnly 'org.postgresql:postgresql'
        implementation 'com.zaxxer:HikariCP'
	// Divers
	developmentOnly 'org.springframework.boot:spring-boot-devtools'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'org.springframework.security:spring-security-test'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
        implementation 'org.springframework.boot:spring-boot-starter-actuator'
	// Oauth 2 JWT et documentation auto api
        // https://mvnrepository.com/artifact/io.jsonwebtoken/jjwt-api
        implementation 'io.jsonwebtoken:jjwt-api:0.12.7'
        // https://mvnrepository.com/artifact/io.jsonwebtoken/jjwt-impl
        runtimeOnly 'io.jsonwebtoken:jjwt-impl:0.12.7'
        // https://mvnrepository.com/artifact/io.jsonwebtoken/jjwt-jackson
        runtimeOnly 'io.jsonwebtoken:jjwt-jackson:0.12.7'
        implementation 'jakarta.xml.bind:jakarta.xml.bind-api:4.0.2'
        // https://mvnrepository.com/artifact/org.springframework.boot/spring-boot-starter-mail
        implementation 'org.springframework.boot:spring-boot-starter-mail:3.5.7'
        // swagger v3 (openApi)
        implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.8.12'
        providedRuntime 'org.springframework.boot:spring-boot-starter-tomcat'
}

tasks.named('test') {
	useJUnitPlatform()
}
